style! = "
: body,html {overflow: hidden}
:
: bbj-app-layout[swapped-drawer] {
: --bbj-app-layout-drawer-width: 120px;
: }
:
: .bbj-toolbar {
:    display: flex;
:    align-items: center;
:    gap: var(--bbj-space-m);
:    padding: 0 var(--bbj-space-m);
: }
:
: .bbj-logo {
:    display: flex;
:    align-items: center;
:    justify-content: center;
:    padding: var(--bbj-space-m) 0;
:    margin-bottom: var(--bbj-space-m);
:    border-bottom: thin solid var(--bbj-color-default)
: }
:
: .bbj-logo img {
:    max-width: 80px;
: }
:
:  bbj-app-layout[swapped-drawer] bbj-tab::part(tab-wrapper) {
:    flex-direction: column;
:    justify-content: center;
:  }
:
:  bbj-app-layout[swapped-drawer] bbj-tab::part(tab-title) {
:    display: flex;
:   flex-direction: column;
:   align-items: center;
:   --bbj-icon-size: 2.5rem;
:  }
:"

use ::BBjAppLayout/BBjAppLayout.bbj::BBjAppLayout

web! = BBjAPI().getWebManager()
rem The app should include a viewport meta tag which contains `viewport-fit=cover`, like the following.
rem This causes the viewport to be scaled to fill the device display.
web!.setMeta("viewport", "width=device-width, initial-scale=1.0, viewport-fit=cover, user-scalable=no")
web!.injectStyle(style!, 0)

sysgui! =  BBjAPI().openSysGui("X0")
imageManager! = sysgui!.getImageManager()

wnd! = sysgui!.addWindow("BBjAppLayout", $01001000$)
wnd!.setCallback(BBjAPI.ON_CLOSE,"eoj")

app! = new BBjAppLayout(wnd!)
app!.setAttribute("swapped-drawer", "true")

rem Header
rem ==================
header! = app!.getHeader()
toolbar! = header!.addChildWindow("", $00108800$, sysgui!.getAvailableContext())
toolbar!.addStyle("bbj-toolbar")
toolbar!.addStaticText("<html><bbj-icon-button name='menu-2' data-drawer-toggle></bbj-icon-button></html>")
toolbar!.addStaticText("<html><h3>DWC Application</h3></html>")

rem Drawer
rem ==================
drawer! = app!.getDrawer()
drawer!.addStyle("bbj-drawer")

logo! = drawer!.addImageCtrl(imageManager!.loadImageFromFile("./assets/logo.png"))
logo!.addStyle("bbj-logo")

empty! = drawer!.addChildWindow("", $00000810$, sysgui!.getAvailableContext())
drawerMenu! = drawer!.addTabCtrl()
drawerMenu!.setCallback(drawerMenu!.ON_TAB_SELECT,"onPageChanged")
drawerMenu!.setAttribute("nobody","true")
drawerMenu!.setAttribute("borderless","true")
drawerMenu!.setAttribute("placement","left")
drawerMenu!.addTab("<span part='tab-title'><bbj-icon name='dashboard'></bbj-icon>      Dashboard</span>"    , empty!)
drawerMenu!.addTab("<span part='tab-title'><bbj-icon name='shopping-cart'></bbj-icon>  Orders</span>"       , empty!)
drawerMenu!.addTab("<span part='tab-title'><bbj-icon name='users'></bbj-icon>          Customers</span>"    , empty!)
drawerMenu!.addTab("<span part='tab-title'><bbj-icon name='box'></bbj-icon>            Products</span>"     , empty!)
drawerMenu!.addTab("<span part='tab-title'><bbj-icon name='files'></bbj-icon>          Documents</span>"    , empty!)
drawerMenu!.addTab("<span part='tab-title'><bbj-icon name='checklist'></bbj-icon>      Tasks</span>"        , empty!)
drawerMenu!.addTab("<span part='tab-title'><bbj-icon name='chart-dots-2'></bbj-icon>   Analytics</span>"    , empty!)

rem Content
rem ==================
content! = app!.getContent()
content!.addStaticText("<html><h1>Application Title</h1></html>")
pageContent! = content!.addStaticText("<html><p>Content goes here</p></html>")

process_events

onPageChanged:
  event! = sysgui!.getLastEvent() 
  title$ = event!.getTitle().replaceAll("<[^>]*>","").trim()

  pageContent!.setText("<html><p>" + title$ + " content goes here</p></html>")
return

eoj:
release
